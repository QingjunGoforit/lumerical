switchtolayout;
deleteall;
clear;
num_wgs = [2];
num_modes = num_wgs*1.5;
############## air trench setting
trench=0; ####trench=0/1, 1 is with trench
###############
width = [4]*1e-6; # can put more #in the array
dist = [5,6,7,8,9]*1e-6;
port = [1]; #indicate which waveguide is extruded to cell 1/3 region
BOX = 4.9e-6;
TOX = 0.5e-6;
height = 0.2e-6;
wl = 1560.61e-9;
Lcoupled = 1E4*1e-3;
dx = 25e-9;
dz = 20e-9;
dy = 40e-9;

length_sweep_start = 1e-3;
length_sweep_end = 10000*1e-3;
span = length_sweep_start:100e-3:length_sweep_end;
        

output_folder = "result_4um_mode_record_noair"; #under current working directory; creat the folder manually
format long; #0.1 // 0.100000000 accuracy setting


for (idx_port=1:length(port))  {
#scan diffrent number of wgs
for (idx=1:length(num_wgs)) {
for (j=1:length(width))  {
    for (i=1:length(dist))  { 
        
    write(output_folder+"/results_Crosstalk_w"+num2str(width(j)*1e9)+"_d"+num2str(dist(i)*1e9)+"_numWGs"+num2str(num_wgs(idx))+"_port"+num2str(port(idx_port))+".csv", "Crosstalk");
    #the column title is cross-talk
    
    switchtolayout;  
    deleteall;

    addrect;
    set("name", "SiO2");
    set("material", "SiO2 (Glass) Ligentec Sellmeier");
    set("y min", -100e-6);        
    set("y max", (width(j) + dist(i))*(num_wgs(idx)-1)+100e-6);
    set("z min", -BOX*2);
    set("z max", height+TOX);
    set("x min", -100e-6);
    set("x max", Lcoupled+100e-6); #the cell2 sio2 layer will stretrch automatically in eme scan
    
    addeme;   
    set("allow custom eigensolver settings", 1);
    set("z min", -BOX/1.2);
    set("z max", height+TOX+2e-6); 
    set("y min", -10e-6);        
    set("y max", (width(j) + dist(i))*(num_wgs(idx)-1)+10e-6);
    set("number of cell groups", 3);
    set("x min",-10e-6);
    set("group spans",[10e-6; Lcoupled; 10e-6]);
    set("cells", [1; 1; 1]);  
    set("Modes",[10; num_modes; 10]);
    set("subcell method",[0; 0; 0]);
    set("y min bc", "metal");
    set("y max bc", "metal");
    set("z min bc", "metal");
    set("z max bc", "metal");
    set("wavelength", wl);
    set("define y mesh by", "maximum mesh step");
    set("define z mesh by", "maximum mesh step");
    set("dy", dy);
    set("dz", dz);

    select("EME::Ports::port_1");
    set("mode selection", "fundamental mode");    
    select("EME::Ports::port_2");
    set("mode selection", "fundamental mode");
    
    ######## add waveguide and trench
    for (k=1:num_wgs(idx))   
    {   
        
        #########add waveguide
        addrect;
        set("name", "wg"+num2str(k));
        set("y", (width(j) + dist(i))*(k-1));        
        set("y span", width(j));
        set("z min", 0);
        set("z max", height);       
            if (k == port(idx_port))    {
            set("x min", -100e-6);
            set("x max", Lcoupled+100e-6);
            set("material", "SiN - Ligentec Sellmeier");
            }
            #extend for idx_port
            else    {
            set("x min", 0);
            set("x max", Lcoupled);
            set("material", "SiN - Ligentec Sellmeier");
            }
            
        addmesh;
        set("dz", dz);
        set("dy", dy);
        set("based on a structure", 1);
        set("structure", "wg"+num2str(k));  
          
        #########add trench
        if(trench==1){
        addrect;
        set("name", "seperate"+num2str(k));
        set("y", (width(j) + dist(i))*(k-1)+dist(i)/2+width(j)/2);        
        set("y span", 1.6e-6);
        set("z min", -(2e-6-height-TOX));
        set("z max", height+TOX);
        set("x min", -100e-6);
        set("x max", Lcoupled+100e-6);
        set("index", 1);
        addrect;
        set("name", "seperate"+num2str(k));
        set("y", (width(j) + dist(i))*(k-1)-dist(i)/2-width(j)/2);        
        set("y span", 1.6e-6);
        set("z min", -(2e-6-height-TOX));
        set("z max", height+TOX);
        set("x min", -100e-6);
        set("x max", Lcoupled+100e-6);
        set("index", 1);
        }
    }
        
        addemeprofile;
        set("monitor type", "2D Z-normal");
        set("x min", -100e-6);
        set("x max", Lcoupled + 100e-6);
        set("y min", -100e-6);        
        set("y max", (width(j) + dist(i))*(num_wgs(idx)-1)+100e-6);
        set("z", height/2);
        set("x resolution", 1000);
        
        ## Running
        run;
 
        ##########save mode
        port1=getresult("EME::Ports::port_1", "mode profiles");
        y=port1.y;
        z=port1.z;
        E=port1.E1;
        Ex=E(:,:,:,1,1);
        Ey=E(:,:,:,1,2);
        Ez=E(:,:,:,1,3);
        port1_filename=output_folder+"/mode_profile_port1_"+num2str(width(j)*1e9)+"_d"+num2str(dist(i)*1e9)+"_numWGs"+num2str(num_wgs(idx))+"_port"+num2str(port(idx_port));
        matlabsave(port1_filename,y,z,Ex,Ey,Ez); 
        
        ##########run one simulation
        emepropagate;  
        #S = getresult("EME", "user s matrix");
        #absS = abs(S); 
        #absS2 = abs(S)^2;
        #S21 = absS2(2,1);
        
        
        ##################EME sweep
        ########## EME sweep settings and run
        setemeanalysis("propagation sweep",1);
        setemeanalysis("parameter","group span 2");
        setemeanalysis("start",length_sweep_start);
        setemeanalysis("stop",length_sweep_end);
        setemeanalysis("number of points",length(span));
        emesweep;
        ########### plot S21 vs group span; save data
        S = getemesweep('S');
        T = abs(S.s21)^2;
        Crosstalk = 10*log10(1-T);
        Crosstalk_loss = -10*log10(T);
        #wavelength = S.wavelength;
        #plot(span, Crosstalk_loss,  "prop length", "dB", "three wgs crosstalk_loss");
        #plot(span, Crosstalk,  "prop length", "dB", "three wgs crosstalk"); 
        write(output_folder+"/results_Crosstalk_w"+num2str(width(j)*1e9)+"_d"+num2str(dist(i)*1e9)+"_numWGs"+num2str(num_wgs(idx))+"_port"+num2str(port(idx_port))+".csv", num2str(Crosstalk));
    }
}  
}
}

        

